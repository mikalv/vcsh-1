# ids_exp.awk
#
# String ID Export 
#
# Inputs : Visual Studio resource ID header file
# Outputs : C source file
#
# This build step generates a table mapping all the Visual
# Studio resource ID's for strings into Scheme keyword names.
# Any resource ID beginning with "IDS_" gets a mapping table
# entry.
#

BEGIN {
  print "/* Generated by ids_exp.awk, DO NOT MODIFY!!! */";
  print "";
  print "struct {";
  print "   UINT _resID;";
  print "   char *_keyword;";
  print "} stringIDTable[] = {";
}

/IDS_/ { 
  string_define = $2;

  string_keyword = tolower($2);

  sub("ids", ":str", string_keyword);

  while (sub("_", "-", string_keyword))
    ;

  print "   { " string_define ", \"" string_keyword "\" },";
}

END {
  print "   {0, NULL}, "
  print "};";
}